SELECT ?station ?stationLabel ?stationID ?coordinate ?transportNetwork ?address ?BaneType ?connectingLine ?openingYear ?geoLatitude ?geoLongitude WHERE {
  ?station wdt:P31 wd:Q55488;  # Instance of train station
          wdt:P17 wd:Q35.     # Country is Denmark
  OPTIONAL { ?station wdt:P722 ?stationID. }           # Danish railway station code
  OPTIONAL { ?station wdt:P625 ?coordinate. }           # Coordinate location
  OPTIONAL { ?station wdt:P16 ?transportNetwork. }   # S-train Copenhagen, aarhus letbane...
  OPTIONAL { ?station wdt:P6375 ?address. }          # address
  OPTIONAL { ?station wdt:P1192 ?BaneType. }            # fx A, B, C... , regional
  OPTIONAL { ?station wdt:P81 ?connectingLine. }         # fx frederikssundbanen, Skagensbanen
  OPTIONAL { ?station wdt:P1619 ?openingYear. }   # Date of official opening
  OPTIONAL { ?station p:P625 ?coord.
    ?coord psv:P625 ?coord_node.
    ?coord_node wikibase:geoLatitude ?geoLatitude;
                wikibase:geoLongitude ?geoLongitude. }    # Latitude and Longitude
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
